def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def devKeystoreProperties = new Properties()
def devKeystorePropertiesFile = rootProject.file('key.dev.properties')
if (devKeystorePropertiesFile.exists()) {
    devKeystoreProperties.load(new FileInputStream(devKeystorePropertiesFile))
}

android {
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 31
        applicationId "cc.exactlylabs.radar.com"
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        dev {
            keyAlias devKeystoreProperties['keyAlias']
            keyPassword devKeystoreProperties['keyPassword']
            storeFile devKeystoreProperties['storeFile'] ? file(devKeystoreProperties['storeFile']) : null
            storePassword devKeystoreProperties['storePassword']
        }

        prod {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    // TODO: Remove this once https://github.com/flutter/flutter/issues/87649
    // is resolved and upgrade Gradle to 7.0.2 in android/build.gradle and android/gradle/wrapper/gradle-wrapper.properties
    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "app"
    productFlavors {
        prod {
            applicationId "cc.exactlylabs.radar.com"
            signingConfig signingConfigs.prod
            manifestPlaceholders = [
                    app_name: "Radar",
                    icon: "@mipmap/ic_launcher"
            ]
        }
        dev {
            applicationId "cc.exactlylabs.radar.com"
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.dev
            manifestPlaceholders = [
                    app_name: "Radar Dev",
                    icon: "@mipmap/ic_launcher"
            ]
        }
        staging {
            applicationId "cc.exactlylabs.radar.com"
            applicationIdSuffix ".staging"
            signingConfig signingConfigs.dev
            manifestPlaceholders = [
                    app_name: "Radar Staging",
                    icon: "@mipmap/ic_launcher"
            ]
        }
    }

    configurations {
        implementation.exclude group: 'org.jetbrains', module: 'annotations'
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    def splashScreen_version = '1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-splashscreen:$splashScreen_version"
}
